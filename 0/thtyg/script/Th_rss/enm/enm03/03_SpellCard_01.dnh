////#東方弾幕風
#ScriptVersion[3]
#Title["桃源宮　stage3　凍符「氷室童子」系"]
#Text[""]
#System["script/Th_rss/system/System_Main.dnh"]
#Image[""]
#Background[""]
	
	//　ライブラリ管理ファイル
	#include "./../lib_Control.dnh"
	#include "./lib_enm03_setting.dnh"
	#include "./lib_IceShot.dnh"
	
	let enm;
	let spl;
	
	//　カウンタ
	let frame		= 0;
	let count		= 0;
	let timer		= 0;
	
	//　制限時間
	let SET_TIMER	= 60;
	
	@Initialize{
		//　ボス生成
		enm	= ObjEnemy_Create(OBJ_ENEMY_BOSS);
		ObjEnemy_SetDamageRate(enm, 0, 0 );
		ObjEnemy_Regist(enm);
		
		//　初期化
		EnemyInitialize(enm);
		
		//　体力0まで待機
		EndEnemy(enm);
		
		//　敵ダメージレート 第2引数:ボムバリア
		SetDamageRate(enm, 80, false );
		ShotScore(enm);
		
		//　スペルカード宣言
		CallSpellCard(enm,0);
		
		//　削除上限
		SetShotAutoDeleteClip(64,64,64,64);
	}
	
	@Loading{
		//　弾データ読み込み　ALPHAとADD
		//LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data.dnh" );
		//LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data_ADD.dnh" );
	}
	
	@MainLoop{
		//　敵の当たり判定
		SetIntersection(enm, true, true );
		
		//　パワー集中エフェクト
		if( timer == 180 ){
			Concentration01_Main(enm, power_rgb, 60 );
		}
		
		//　弾幕
		if( timer == 280 ){
			ShotMain(enm);
		}
		
		//　カウンタ
		if( !Obj_IsDeleted(enm) ){
			count++;
			frame++;
			timer++;
		}
		else{
			//　0でなんか発動すると困るので変な数値にする
			count	= -52149;
			frame	= -52149;
			timer	= -52149;
		}
		
		yield;
	}
	
	@Event{
		alternative( GetEventType() )
		case( EV_REQUEST_LIFE ){
			//　ライフ一律1000
			SetScriptResult( 1000 );
		}
		case( EV_REQUEST_TIMER ){
			//　制限時間
			SetScriptResult( SET_TIMER );
		}
		case( EV_REQUEST_SPELL_SCORE ){
			//　スペルカードボーナス
			SetScriptResult( SET_SCORE );
		}
		case( EV_GAIN_SPELL ){
			//　スペル取得
			SpellCounterGet( GetDifficult, 3, 1 );
		}
	}
	
	@Finalize{ }
	
	//----------------------------------------------------------------------------------------------------
	//----------------------------------------------------------------------------------------------------
	//　task色々
	
	task EnemyInitialize(ID){
		//　アニメーション用共通データ作成
		CreateCommonDataArea("anime");
		SetAreaCommonData("anime","spa",false);
		
		//　60角形　距離64　優先71　遅延120.f　色[紫]　スペル用　[外枠出現処理ON,本体出現処理ON]
		//　"Default" or "Normal" or "SpellCard"
		CreateLifeBar(ID, 60, 64, 71, 0, [255,255,255], "SpellCard" , [false,false] );
		//　type　delay
		//　0:戦闘開始　1:戦闘継続
		RedMagicCircle( 1, 0 );
		Aura(ID, 8, aura_rgb[0], aura_rgb[1] );
		//　親ID　表示遅延　bool 移動効果　char 移動効果種別　bool フェードイン
		//　"BeginSpellCard" or "EndSpellCard"
		SetDrawTimer(enm, 0, true, "BeginSpellCard", false );
		//　画像パスだけ変えればいいんじゃないかな
		EnemyDisplay02(enm, true, enemy_tex_path );
		ObjMove_SetDestAtWeight(enm, CenX, CenY-96, 8, 8 );
	}
	
	task CallSpellCard(ID,long){
		wait(long);
		spl	= GetEnemyBossSceneObjectID();
		ObjEnemyBossScene_StartSpell( spl );
		
		let spell_text = "";
		
		alternative( GetDifficult )
		case( "Easy", "Normal" ){
/* to be translated */			spell_text = "凍符「氷室童子」";
		}
		case( "Hard" ){
/* to be translated */			spell_text = "氷棺「アイスユキンコフィン」";
		}
		case( "Lunatic" ){
/* to be translated */			spell_text = "邪神「シャイコースの白光」";
		}
		case( "OverDrive" ){
/* to be translated */			spell_text = "邪神「シャイコースの白光」";
		}
		
		SpellCounter( GetDifficult, 3, 1 );
		let sct = GetSpellCounter( GetDifficult, 3, 1 );
		
		CutinSP2(ID, spell_text, 0, 0, true );
		SpellCardCutin(ID, "TIREIDEN", 220, 192, [1,1], enemy_cutin_path2, 512, 512, true );
		SpellCardBonusDisplay(spl,ID );
		SpellCardHistoryDisplay(ID, sct[1], sct[0] );
		SetBackGroundEffect_03_R(ID);
		SpellCardMagicTimer(ID);
	}
	
	function wait( long ){
		loop( long ){ yield; }
	}
	
	//　ショットメイン
	task ShotMain( ID ){
		let count	= 0;
		let frame	= 0;
		let i		= 0;
		let j		= 0;
		
		//　way数
		let lp		= [ 0, 0 ];
		//　角度
		let angle	= 0;
		let agp		= 180;
		//　レーザー回数
		let lmax	= 6;
		//　レーザー発射間隔
		let wtA		= 30;
		//　レーザーカラー
		let rgb_array = [
			[64,64,255],	//　青
			[64,128,255],	//　深い水
			[64,192,255],	//　淡い青
			[64,255,255],	//　水
			[128,64,255],	//　青紫
			[192,64,255],	//　青紫
		];
		
		alternative( GetDifficult )
		case( "Easy" ){
			lp		= [ 3, 3 ];
			wtA		= 60;
			lmax	= 3;
		}
		case( "Normal" ){
			lp		= [ 3, 5 ];
			wtA		= 45;
			lmax	= 4;
		}
		case( "Hard" ){
			lp		= [ 4, 5 ];
			agp = 45;
		}
		case( "Lunatic" ){
			lp		= [ 5, 5 ];
			agp = 36;
		}
		case( "OverDrive" ){
			lp		= [ 3, 7 ];
		}
		
		while( !Obj_IsDeleted( ID ) ){
			let gpos	= [ ObjMove_GetX(ID), ObjMove_GetY(ID) ];
			
			//　パワー溜め
			if( count == 300 ){
				Concentration01_Main(enm, power_rgb, 60 );
				EnemyMove(enm, 2, 8 );
				count = 0;
				frame = 0;
				angle += agp;
				j = 0;
			}
			
			if( frame > 100 && frame%wtA == 0 && j < lmax ){
				ascent( i in 0..(lp[0]) ){
					let ang = 270+i*(360/lp[0]) + angle + j*180;
					let spos = [
						gpos[0] + ( frame ) * cos( ang ),
						gpos[1] + ( frame ) * sin( ang ),
					];
					CreateLaser( enm, spos, ang + j*180, rgb_array[ j ] );
				}
				ObjSound_Play( CreateShotSE[11] );
				j++;
				SetAreaCommonData("anime", "spa", false );
			}
			
			if( frame == 0 ){
				ascent( i in 0..(lp[1]) ){
					CreateIceShot( ID, gpos, 90 + i*(360/lp[1]), 3, -0.05, 1.6, 0.5, 180 );
				}
				ObjSound_Play( CreateShotSE[0] );
				SetAreaCommonData("anime", "spa", true );
			}
			
			if( frame == 240 && GetDifficult != "Easy" && GetDifficult != "Normal" ){
				ascent( let j2 in 0..6 ){
					ascent( i in 0..(lp[0]) ){
						let ang = 270+i*(360/lp[0]) + angle + j2*180;
						let spos = [
							gpos[0] + (72+j2*36) * cos( ang ),
							gpos[1] + (72+j2*36) * sin( ang ),
						];
						ascent( let i2 in 0..(lp[1]) ){
							let sht = CreateShotA2( spos[0], spos[1], 4, ang + i2*(360/lp[1]), -0.4, 0, BGW_AQUA_08, 32 );
							NotifyEventAll( EV_USER + 40, [ sht, 32 ] );
							ShotDelete( ID, sht, 300 );
						}
					}
				}
				ObjSound_Play( CreateShotSE[0] );
			}
			
			count++;
			frame++;
			
			yield;
		}
	}
	
	task CreateLaser( CallID, pos, angle, rgb ){
		if( GetDifficult == "Easy" || GetDifficult == "Normal" ){
			ascent( let i in 0..3 ){
				CreateStraightLaser01( CallID, pos, angle + i*120, 640, 12, rgb, true, 100, 160 );
			}
		}
		else{
			ascent( let i in 0..3 ){
				CreateStraightLaser01( CallID, pos, angle + i*120, 640, 12, rgb, true, 60, 120 );
			}
		}
	}
	
	task CreateIceShot( CallID, pos, angle, speed, acc, mx, mo, limit ){
		let sht		= CreateShotA2( pos[0], pos[1], speed, angle, acc, 0, ADD_BGW_WHITE_03, 16 );
		NotifyEventAll( EV_USER + 40, [ sht, 16 ] );
		let count	= 0;
		
		while( !Obj_IsDeleted(CallID) && !Obj_IsDeleted(sht) && ObjMove_GetSpeed(sht) > 0 ){
			yield;
		}
		
		ObjMove_SetAcceleration(sht, 0.01 );
		ObjMove_SetMaxSpeed(sht, mx );
		
		ShotChild( CallID, sht );
		
		while( !Obj_IsDeleted(CallID) && !Obj_IsDeleted(sht) && count < limit ){
			
			let angleToPlayer = atan2( GetPlayerY()-ObjMove_GetY(sht), GetPlayerX-ObjMove_GetX(sht) );
			
			if( sin(angleToPlayer - ObjMove_GetAngle(sht)) > 0 ){
				ObjMove_SetAngle(sht, ObjMove_GetAngle(sht) + mo );
			}
			else{
				ObjMove_SetAngle(sht, ObjMove_GetAngle(sht) - mo );
			}
			
			count++;
			
			yield;
		}
	}
	
	task ShotChild( CallID, ObjID ){
		let count	= 0;
		
		while( !Obj_IsDeleted(CallID) && !Obj_IsDeleted(ObjID) ){
			if( count > 0 && count%8 == 0 ){
				let dis = rand(0,16);
				let ang = rand(0,360);
				let spos = [
					ObjMove_GetX(ObjID) + dis * cos( ang ),
					ObjMove_GetY(ObjID) + dis * sin( ang ),
				];
				let sht2 = CreateShotA2( spos[0], spos[1], rand(1,4), ObjMove_GetAngle(ObjID)+180, -0.2, 0, ADD_BGW_WHITE_13, 8 );
				NotifyEventAll( EV_USER + 40, [ sht2, 8 ] );
				if( GetDifficult == "Easy" || GetDifficult == "Normal" ){
					ShotDelete( CallID, sht2, 120 );
				}
				else{
					ShotDelete( CallID, sht2, 60 );
				}
				ObjSound_Play( CreateShotSE[1] );
			}
			
			count++;
			
			yield;
		}
	}
	
	task ShotDelete( CallID, ObjID, long ){
		//　削除エフェクトだけ出るようにする
		Obj_SetValue( ObjID, "Item_Off", true );
		//　速度が 0 になるまで待機
		while( !Obj_IsDeleted(ObjID) && ObjMove_GetSpeed(ObjID) > 0 ){ yield; }
		//　氷結
		IceEffect( CallID, ObjID, rand(0.3,0.5) );
		//　削除まで long フレーム待機
		loop( long ){
			if( Obj_IsDeleted(ObjID) ){ break; }
			yield;
		}
		//　アイテムとして削除
		ObjShot_ToItem( ObjID );
	}
	
