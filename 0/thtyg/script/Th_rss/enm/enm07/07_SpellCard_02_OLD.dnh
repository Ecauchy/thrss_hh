//#東方弾幕風
#ScriptVersion[3]
#Title["桃源宮　stage7　二首「蛇の目念動ミシン」"]
#Text[""]
#System["script/Th_rss/system/System_Main.dnh"]
#Image[""]
#Background[""]
	
	//　ライブラリ管理ファイル
	#include "./../lib_Control.dnh"
	#include "./lib_enm07_setting.dnh"
	#include "./lib_enm07_effect.dnh"
	
	let enm;
	let spl;
	
	//　カウンタ
	let count		= 0;
	
	//　制限時間
	let SET_TIMER	= 60;
	
	@Initialize{
		//　ボス生成
		enm	= ObjEnemy_Create(OBJ_ENEMY_BOSS);
		ObjEnemy_SetDamageRate( enm, 0, 0 );
		ObjEnemy_Regist( enm );
		
		//　初期化
		EnemyInitialize( enm );
		
		//　体力0まで待機
		EndEnemy( enm );
		
		//　敵ダメージレート 第2引数:ボムバリア
		SetDamageRate( enm, 100, true );
		ShotScore( enm );
		
		//　スペルカード宣言
		CallSpellCard( enm, 0 );
		
		//　削除上限
		SetShotAutoDeleteClip( 64, 64, 64, 64 );
	}
	
	@Loading{
		//　弾データ読み込み　ALPHAとADD
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data.dnh" );
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data_ADD.dnh" );
	}
	
	@MainLoop{
		//　敵の当たり判定
		SetIntersection( enm, true, true );
		
		//　弾幕
		if( count == 180 ){
			ShotMain( enm );
		}
		
		//　カウンタ
		count++;
		
		yield;
	}
	
	@Event{
		alternative( GetEventType() )
		case( EV_REQUEST_LIFE ){
			SetScriptResult( 1000 );
		}
		case( EV_REQUEST_TIMER ){
			//　制限時間
			SetScriptResult( SET_TIMER );
		}
		case( EV_REQUEST_SPELL_SCORE ){
			//　スペルカードボーナス
			SetScriptResult( SET_SCORE );
		}
		case( EV_GAIN_SPELL ){
			//　スペル取得
			//SpellCounterGet( GetDifficult, 6, 1 );
		}
	}
	
	@Finalize{ }
	
	//----------------------------------------------------------------------------------------------------
	//----------------------------------------------------------------------------------------------------
	
	//　初期化
	task EnemyInitialize(ID){
		//　アニメーション用共通データ作成
		CreateCommonDataArea( "anime" );
		SetAreaCommonData( "anime", "spa", false );
		
		//　60角形　距離64　優先71　遅延120.f　色[紫]　スペル用　[外枠出現処理ON,本体出現処理ON]
		//　"Default" or "Normal" or "SpellCard"
		CreateLifeBar( ID, 60, 64, 71, 0, [255,255,255], "SpellCard" , [false,false] );
		//　type　delay
		//　0:戦闘開始　1:戦闘継続
		RedMagicCircle( 1, 0 );
		Aura( ID, 8, aura_rgb[0], aura_rgb[1] );
		//　親ID　表示遅延　bool 移動効果　char 移動効果種別　bool フェードイン
		//　"BeginSpellCard" or "EndSpellCard"
		SetDrawTimer( enm, 0, true, "BeginSpellCard", false );
		//　画像パスだけ変えればいいんじゃないかな
		EnemyDisplay_Enm06a( enm, true, enemy_tex_path );
		//　敵位置初期化
		ObjMove_SetDestAtWeight( enm, CenX, CenY-96, 8, 8 );
	}
	
	//　スペカ宣言
	task CallSpellCard(ID,long){
		Wait(long);
		spl	= GetEnemyBossSceneObjectID();
		ObjEnemyBossScene_StartSpell( spl );
		
		let spell_text = "";
		alternative( GetDifficult )
		case( "Extra" ){
/* to be translated */			spell_text = "二首「蛇の目念動ミシン」";
		}
		others{
/* to be translated */			spell_text = "二首「蛇の目念動ミシン」";
		}
		
		//SpellCounter( GetDifficult, 6, 2 );
		//let sct = GetSpellCounter( GetDifficult, 6, 2 );
		
		CutinSP2(ID, spell_text, 0, 0, true );
		SpellCardCutin(ID, "TIREIDEN", 220, 192, [1,1], enemy_cutin_path, 512, 512, true );
		SpellCardBonusDisplay(spl,ID );
		//SpellCardHistoryDisplay(ID, sct[1], sct[0] );
		SetBackGroundEffect_07b(ID);
		SpellCardMagicTimer(ID);
	}
	
	//　遅延
	function Wait( long ){
		loop( long ){
			yield;
			if( Obj_IsDeleted( enm ) ){ return true; }
		}
		return false;
	}
	
	//　ショットメイン
	task ShotMain( ID ){
		Concentration03_Main( ID, power_rgb, 60 );
		Wait( 100 );
		Shot01( ID, 4 );
		Shot01( ID, -4 );
		
		while( !Obj_IsDeleted(ID) ){
			
			Wait( 180 );
			
			EnemyMove( ID, 2, 8 );
			
			yield;
		}
	}
	
	task Shot01( ID, m ){
		let pos		= [ ObjMove_GetX(ID), ObjMove_GetY(ID) ];
		let sht		= CreateLaserC( pos[0], pos[1], 90, 0, 0, 0, 10, [ 32, 255, 32 ], 0.01, 2, ADD_BGW_WHITE_30 );
		let frame	= 0;
		let d		= 2;
		
		ObjShot_SetAutoDelete( sht, false );
		ObjShot_SetSpellResist( sht, true );
		
		while( !Obj_IsDeleted(sht) && !Obj_IsDeleted(ID) ){
			let gpos = [ ObjMove_GetX(sht), ObjMove_GetY(sht) ];
			
			let angleToPlayer = GetGapAngle(
				gpos,
				[ GetPlayerX(), GetPlayerY() ]
			);
			
			ObjMove_SetSpeed( sht, 3 + 1 * sin( frame ) );
			
			if( sin(angleToPlayer - ObjMove_GetAngle(sht)) > 0 ){
				ObjMove_SetAngle( sht, ( ObjMove_GetAngle(sht) + d ) + m * sin( frame*6 ) );
			}
			else{
				ObjMove_SetAngle( sht, ( ObjMove_GetAngle(sht) - d ) + m * sin( frame*6 ) );
			}
			
			if( frame%4 == 0 ){
				let obj = CreateShot01( gpos, 0, ObjMove_GetAngle(sht) + 180 + rand(-16,16), 0.01, 1, BGB_GREEN_14, 16 );
				ShotDelete( ID, obj, 150 );
				ObjSound_Play( CreateShotSE[12] );
			}
			
			if( frame%360 == 0 ){
				let lp = 16;
				ascent( let i in 0..(lp) ){
					let obj = CreateShot01( gpos, 4, ObjMove_GetAngle(sht) + i*(360/lp), -0.2, 0.6, BGW_PURPLE_27, 16 );
				}
				ObjSound_Play( CreateShotSE[0] );
			}
			
			frame++;
			
			yield;
		}
		Obj_Delete(sht);
	}
	
