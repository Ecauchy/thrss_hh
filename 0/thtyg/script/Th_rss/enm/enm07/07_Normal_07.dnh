//#東方弾幕風
#ScriptVersion[3]
#Title["桃源宮　stage7　やと通常07"]
#Text[""]
#System["script/Th_rss/system/System_Main.dnh"]
#Image[""]
#Background[""]
	
	//　ライブラリ管理ファイル
	#include "./../lib_Control.dnh"
	#include "./lib_enm07_setting.dnh"
	#include "./lib_enm07_effect.dnh"
	
	let enm;
	let spl;
	
	//　カウンタ
	let frame		= 0;
	
	//　制限時間
	let SET_TIMER	= 51;
	
	@Initialize{
		//　ボス生成
		enm	= ObjEnemy_Create(OBJ_ENEMY_BOSS);
		ObjEnemy_SetDamageRate(enm, 0, 0 );
		ObjEnemy_Regist(enm);
		
		//　初期化
		EnemyInitialize(enm);
		
		//　体力0まで待機
		EndEnemy(enm);
		
		//　敵ダメージレート 第2引数:ボムバリア
		SetDamageRate(enm, 100, false );
		ShotScore(enm);
		
		//　削除上限
		SetShotAutoDeleteClip( 32, 32, 32, 32 );
	}
	
	@Loading{
		//　弾データ読み込み　ALPHAとADD
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data.dnh" );
		LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data_ADD.dnh" );
	}
	
	@MainLoop{
		//　敵の当たり判定
		SetIntersection(enm, true, true );
		
		//　パワー集中エフェクト
		if( frame == 180 ){
			Concentration03_Main( enm, power_rgb, 60 );
		}
		
		//　弾幕
		if( frame == 280 ){
			ShotMain(enm);
		}
		
		frame++;
		
		yield;
	}
	
	@Event{
		alternative( GetEventType() )
		case( EV_REQUEST_LIFE ){
			SetScriptResult( 1500 );
		}
		case( EV_REQUEST_TIMER ){
			//　制限時間
			SetScriptResult( SET_TIMER );
		}
		case( EV_REQUEST_SPELL_SCORE ){
			//　スペルカードボーナス
			SetScriptResult( SET_SCORE );
		}
	}
	
	@Finalize{ }
	
	//----------------------------------------------------------------------------------------------------
	//----------------------------------------------------------------------------------------------------
	//　task色々
	
	task EnemyInitialize(ID){
		//　アニメーション用共通データ作成
		CreateCommonDataArea("anime");
		SetAreaCommonData("anime","spa",false);
		
		//　60角形　距離64　優先71　遅延120.f　色[紫]　スペル用　[外枠出現処理ON,本体出現処理ON]
		//　"Default" or "Normal" or "SpellCard"
		CreateLifeBar(ID, 60, 64, 71, 0, [255,255,255], "Normal" , [false,true] );
		//　type　delay
		//　0:戦闘開始　1:戦闘継続
		RedMagicCircle( 1, 0 );
		Aura(ID, 8, aura_rgb[0], aura_rgb[1] );
		//　親ID　表示遅延　bool 移動効果　char 移動効果種別　bool フェードイン
		//　"BeginSpellCard" or "EndSpellCard"
		SetDrawTimer(enm, 0, false, "EndSpellCard", true );
		//　画像パスだけ変えればいいんじゃないかな
		EnemyDisplay_Enm07b(enm, true, enemy_tex_path );
		ObjMove_SetDestAtWeight(enm, CenX, CenY-96, 8, 8 );
	}
	
	//　遅延
	function Wait( long ){
		loop( long ){
			yield;
			if( Obj_IsDeleted( enm ) ){ return true; }
		}
		return false;
	}
	
	//　ショットメイン
	task ShotMain( ID ){
		let r = 1 - round( rand( 0, 1 ) ) * 2;
		while( !Obj_IsDeleted(ID) ){
			
			let av = -0.2;
			let way = 6;
			let wide = 180;
			let speed = 5;
			let angle = GetAngleToPlayer( enm ) + 180 - wide / 2 * r;
			loop( way ){
				Shot01( ID, speed, angle, av, -speed/50, 0, [ ADD_BGW_PURPLE_02, BGW_AQUA_31 ], 32, 120, [32,255,255] );
				ObjSound_Play( CreateShotSE[1] );
				angle += wide / ( way - 1 ) * r;
				if( Wait( 6 ) ){ break; }
			}
			
			EnemyMove( ID, 2, 8 );
			
			if( Wait( 120 ) ){ break; }
			r = -r;
		}
		
	}
	
	task Shot01( ID, speed, angle, av, acc, maxspeed, shote, delay, wait, color ){
		let gpos	= [ ObjMove_GetX(ID), ObjMove_GetY(ID) ];
		let sht		= CreateShot01( gpos, speed, angle, acc, maxspeed, shote[0], delay );
		ObjShot_SetSpellResist( sht, true );
		Wait( wait );
		
		let lp = 8;
		let rd = 16;
		let ra = 90;
		
		if( Obj_IsDeleted(sht) ){ return; }
		ascent( let i in 0..lp ){
			let ag = 360/lp;
			let spos = [
				ObjMove_GetX(sht) + rd * cos( ra+i*ag ),
				ObjMove_GetY(sht) + rd * sin( ra+i*ag ),
			];
			let speed = 3;
			loop( 2 ){
				let obj = CreateShot01( spos, speed, ra+i*ag, -speed/60, speed/2, shote[1], 8 );
				ObjMove_SetAngularVelocity( obj, av );
				speed /= 1.4;
			}
		}
		ObjSound_Play( CreateShotSE[1] );
		
		local{
			let spos = [ ObjMove_GetX(sht), ObjMove_GetY(sht) ];
			let wide = 320;
			let angle = GetAngleToPlayer( sht ) - wide / 2;
			loop( 3 ){
				SetHormingLaser( spos, angle );
				angle += wide / 2;
			}
			ObjSound_Play( CreateShotSE[3] );
		}
		
		Obj_Delete( sht );

		task SetHormingLaser( gpos, angle ){
			let sht		= CreateLaserC( gpos[0]+32*cos(angle), gpos[1]+32*sin(angle), angle, 0, 0, 0, 16, [ 224, 64, 224 ], 0.06, 1, ADD_BGW_WHITE_30 );
			let count	= 0;
			let speed	= 0;
			ObjShot_SetAutoDelete( sht, false );
			ObjShot_SetSpellResist( sht, true );
			let sbase	= 3.6;
			while( !Obj_IsDeleted(sht) ){
				let spos = [ ObjMove_GetX(sht), ObjMove_GetY(sht) ];
				let mpos = [ ObjMove_GetX( GetPlayerObjectID ), ObjMove_GetY( GetPlayerObjectID ) ];
				let dangle = absolute( 6 - min( speed, 6 ) );
				let dmax = 1;
				if( dangle >= dmax ){ dangle = dmax; }
				let toAngle = GetGapAngle( spos, mpos );
				let ret = toAngle - angle;
				if( ( ret > 0 && ret <= 180 ) || ( ret <= -180 ) ){ angle += dangle; }
				else if( ( ret > 180 ) || ( ret < 0 && ret > - 180 ) ){ angle += -dangle; }
				if( angle >= 180 ){ angle += -360; }
				if( angle < -180 ){ angle += 360; }

				if( sbase > 0 ){
					sbase -= 0.1;
					speed = sbase;
				}else{
					speed += 0.05;
					let v = 96;
					if( spos[0] < MinX-v || spos[0] > MaxX+v || spos[1] < MinY-v || spos[1] > MaxY+v ){
						break;
					}
				}

				let sx = spos[0] + speed * cos( angle );
				let sy = spos[1] + speed * sin( angle );
				ObjMove_SetPosition( sht, sx, sy );
				ObjMove_SetAngle( sht, angle );
				
				count++;
				if( Wait( 1 ) ){ return; }
			}
			Obj_Delete(sht);
		}
	}

