////#東方弾幕風
#ScriptVersion[3]
#Title["桃源宮　stage2　桜符「穴あき桜花結界」系"]
#Text[""]
#System["script/Th_rss/system/System_Main.dnh"]
#Image[""]
#Background[""]
	
	//　ライブラリ管理ファイル
	#include "./../lib_Control.dnh"
	#include "./lib_enm02_setting.dnh"
	
	let enm;
	let spl;
	
	//　カウンタ
	let frame		= 0;
	let count		= 0;
	let timer		= 0;
	
	//　制限時間
	let SET_TIMER	= 30;
	
	@Initialize{
		//　ボス生成
		enm	= ObjEnemy_Create(OBJ_ENEMY_BOSS);
		ObjEnemy_SetDamageRate(enm, 0, 0 );
		ObjEnemy_Regist(enm);
		
		//　初期化
		EnemyInitialize(enm);
		
		//　体力0まで待機
		EndEnemy(enm);
		
		//　敵ダメージレート 第2引数:ボムバリア
		SetDamageRate(enm, 70, false );
		ShotScore(enm);
		
		//　スペルカード宣言
		CallSpellCard(enm,0);
		
		//　削除上限
		SetShotAutoDeleteClip(64,128,64,64);
	}
	
	@Loading{
		//　弾データ読み込み　ALPHAとADD
		//LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data.dnh" );
		//LoadEnemyShotData( csd ~ "./../../lib/lib_bullet_data_ADD.dnh" );
	}
	
	@MainLoop{
		//　敵の当たり判定
		SetIntersection(enm, true, true );
		
		//　パワー集中エフェクト
		if( timer == 180 ){
			Concentration01_Main(enm, power_rgb_C, 60 );
		}
		
		//　弾幕
		if( timer == 240 ){
			ShotMain(enm);
			SetAreaCommonData("anime", "spa", true );
		}
		
		//　カウンタ
		if( !Obj_IsDeleted(enm) ){
			count++;
			frame++;
			timer++;
		}
		else{
			//　0でなんか発動すると困るので変な数値にする
			count	= -52149;
			frame	= -52149;
			timer	= -52149;
		}
		
		yield;
	}
	
	@Event{
		alternative( GetEventType() )
		case( EV_REQUEST_LIFE ){
			SetScriptResult( 800 );
		}
		case( EV_REQUEST_TIMER ){
			//　制限時間
			SetScriptResult( SET_TIMER );
		}
		case( EV_REQUEST_SPELL_SCORE ){
			//　スペルカードボーナス
			SetScriptResult( SET_SCORE );
		}
		case( EV_GAIN_SPELL ){
			//　スペル取得
			SpellCounterGet( GetDifficult, 2, 0 );
		}
	}
	
	@Finalize{ }
	
	//----------------------------------------------------------------------------------------------------
	//----------------------------------------------------------------------------------------------------
	//　task色々
	
	task EnemyInitialize(ID){
		//　アニメーション用共通データ作成
		CreateCommonDataArea("anime");
		SetAreaCommonData("anime","spa",false);
		
		//　60角形　距離64　優先71　遅延120.f　色[紫]　スペル用　[外枠出現処理ON,本体出現処理ON]
		//　"Default" or "Normal" or "SpellCard"
		CreateLifeBar(ID, 60, 64, 71, 0, [255,255,255], "SpellCard" , [false,false] );
		//　type　delay
		//　0:戦闘開始　1:戦闘継続
		RedMagicCircle( 1, 0 );
		Aura(ID, 8, aura_rgb_C[0], aura_rgb_C[1] );
		//　親ID　表示遅延　bool 移動効果　char 移動効果種別　bool フェードイン
		//　"BeginSpellCard" or "EndSpellCard"
		SetDrawTimer(enm, 0, true, "BeginSpellCard", false );
		//　画像パスだけ変えればいいんじゃないかな
		EnemyDisplay01(enm, true, enemy_tex_path_C[0] );
		ObjMove_SetDestAtWeight(enm, CenX, CenY-96, 8, 8 );
	}
	
	task CallSpellCard(ID,long){
		wait(long);
		spl	= GetEnemyBossSceneObjectID();
		ObjEnemyBossScene_StartSpell( spl );
		
		let spell_text = "";
		
		alternative( GetDifficult )
		case( "Easy", "Normal" ){
/* to be translated */			spell_text = "桜符「穴あき桜花結界」";
		}
		case( "Hard" ){
/* to be translated */			spell_text = "桜符「桜天門の変」";
		}
		case( "Lunatic" ){
/* to be translated */			spell_text = "桜符「桜天門の変」";
		}
		case( "OverDrive" ){
/* to be translated */			spell_text = "桜符「桜天門の変」";
		}
		
		SpellCounter( GetDifficult, 2, 0 );
		let sct = GetSpellCounter( GetDifficult, 2, 0 );
		
		CutinSP2(ID, spell_text, 0, 0, true );
		SpellCardCutin(ID, "TIREIDEN", 220, 192, [1,1], enemy_cutin_path_C[0], 512, 512, true );
		SpellCardBonusDisplay(spl,ID );
		SpellCardHistoryDisplay(ID, sct[1], sct[0] );
		SetBackGroundEffect_02b(ID);
		SpellCardMagicTimer(ID);
	}
	
	function wait( long ){
		loop( long ){ yield; }
	}
	
	//　ショットメイン
	task ShotMain( ID ){
		let frame	= 0;
		let count	= 0;
		let i		= 0;
		
		//　way数
		let lp		= [ 0, 0 ];
		//　ばら撒き発射間隔
		let wt		= 32;
		//　角度補正
		let agp		= 2;
		
		let shote	= [ ADD_BGW_PURPLE_18, ADD_BGW_RED_18 ];
		let type	= 0;
		let angle	= 0;
		let delay	= 0;

		let angle_p	= GetAngleToPlayer( ID );
		
		alternative( GetDifficult )
		case( "Easy" ){
			lp		= [ 3, 5 ];
		}
		case( "Normal" ){
			lp		= [ 6, 8 ];
		}
		case( "Hard" ){
			lp		= [ 9, 8 ];
			agp = 1.75;
		}
		case( "Lunatic" ){
			lp		= [ 13, 10 ];
			wt		= 20;
			agp = 1.5;
		}
		case( "OverDrive" ){
			lp		= [ 7, 20 ];
		}
		
		while( !Obj_IsDeleted( ID ) ){
			let gpos	= [ ObjMove_GetX(ID), ObjMove_GetY(ID) ];
			
			//　パワー溜めから初期化
			if( frame == 220 ){
				Concentration01_Main(enm, power_rgb_C, 60 );
				frame = 0;
				count = 0;
				angle = 0;
				angle_p = GetAngleToPlayer( ID );
				delay = 0;
				type++;
			}
			
			//　フラクタル
			if( type%2 == 0 ){
				if( count%(1+delay) == 0 ){
					ascent( i in 0..(lp[0]) ){
						let sht1 = CreateShotA2( gpos[0], gpos[1], 3, angle_p + i*(360/lp[0]) - angle, -0.1, 1.6, shote[ type%2 ], 16 );
						let sht2 = CreateShotA2( gpos[0], gpos[1], 3, angle_p + i*(360/lp[0]) + angle, -0.1, 1.6, shote[ type%2 ], 16 );
						NotifyEventAll( EV_USER + 40, [ sht1, 16 ] );
						NotifyEventAll( EV_USER + 40, [ sht2, 16 ] );
					}
					ObjSound_Play( CreateShotSE[1] );
					angle += agp;
					if( frame > 2 ){ delay += 4; }
					count = 0;
				}
			}
			else{
				if( count%(32-delay) == 0 ){
					ascent( i in 0..(lp[0]) ){
						let sht1 = CreateShotA2( gpos[0], gpos[1], 3, 270 + i*(360/lp[0]) - angle, -0.1, 1.6, shote[ type%2 ], 16 );
						let sht2 = CreateShotA2( gpos[0], gpos[1], 3, 270 + i*(360/lp[0]) + angle, -0.1, 1.6, shote[ type%2 ], 16 );
						NotifyEventAll( EV_USER + 40, [ sht1, 16 ] );
						NotifyEventAll( EV_USER + 40, [ sht2, 16 ] );
					}
					ObjSound_Play( CreateShotSE[1] );
					angle += agp;
					if( frame > 8 ){ delay += 2; }
					count = 0;
				}
			}
			
			//　nway ばら撒き
			if( frame > 0 && frame%wt == 0 ){
				let angle = rand(0,360);
				ascent( i in 0..(lp[1]) ){
					let ang = i*(360/lp[1]) + angle + (360/lp[1]/2);
					let spos = [
						gpos[0] + 24 * cos( ang ),
						gpos[1] + 24 * sin( ang ),
					];
					let sht = CreateShotA2( spos[0], spos[1], 0, ang, 0.01, 2, BGB_RED_11, 16 );
					NotifyEventAll( EV_USER + 40, [ sht, 16 ] );
				}
				ObjSound_Play( CreateShotSE[2] );
			}
			
			frame++;
			count++;
			
			yield;
		}
	}
	
